// For building the open-source release of TalkBack.

apply plugin: 'com.android.application'

apply from: 'shared.gradle'

apply from: 'version.gradle'

dependencies {
    implementation project(':talkback')
}

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.8.1'
        classpath 'com.android.tools.build:gradle:3.5.4'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

android {
    buildToolsVersion '29.0.0'
    defaultConfig {
        applicationId talkbackApplicationId
        versionName talkbackVersionName
        manifestPlaceholders = [talkbackMainPermission:talkbackMainPermission]
        minSdkVersion 26
        targetSdkVersion 30
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
        multiDexEnabled true
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'README'
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

task createTranslationTablesZip(type: Zip){
    archiveName="translationtables.zip"
    destinationDir = file('braille/src/main/res/raw/')
    from fileTree('./braille/src/main/tables/')
    into('liblouis/tables')
}

preBuild.dependsOn createTranslationTablesZip
